plugins {
    id 'java-library'
    id "java-test-fixtures"
    id "me.champeau.jmh" version "0.7.0"
    id "com.diffplug.spotless" version "6.17.0"
    id 'maven-publish'
    id 'org.jreleaser' version '1.12.0'
}

group = 'io.github.crate-crypto.peerdas-kzg'
version = '0.0.1' // x-release-please-version


java {
  withJavadocJar()
  withSourcesJar()
}

repositories {
    mavenCentral()
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    def jacksonVersion = "2.14.2"

    testFixturesImplementation("org.apache.tuweni:tuweni-units:2.3.1")
    testFixturesImplementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    testFixturesImplementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}")
}

// TODO: Re-evaluate whether we should keep this
test {
    useJUnitPlatform()
    dependsOn cleanTest
    testLogging.showStandardStreams = true
}
        
publishing {
  publications {
    maven(MavenPublication) {
      from components.java

      pom {
        name = 'PeerDAS KZG Library'
        description = 'A library for working with PeerDASKZG'
        url = 'https://github.com/crate-crypto/peerdas-kzg'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'kevthedev'
            name = 'Kevaundray Wedderburn'
            email = 'kev@the.dev'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/crate-crypto/peerdas-kzg.git'
          developerConnection = 'scm:git:ssh://github.com:crate-crypto/peerdas-kzg.git'
          url = 'https://github.com/crate-crypto/peerdas-kzg'
        }
      }
    }
  }

  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}
        
jreleaser {
  // Jreleaser will look for .git and its at the top level repository
  gitRootSearch = true
  
  signing {
    active = 'ALWAYS'
    armored = true
  }
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

// TODO: Find out why jreleaser does not just create this
// Directory.
tasks.register('createJReleaserOutputDir') {
  doLast {
    mkdir 'build/jreleaser'
  }
}

tasks.named('jreleaserFullRelease') {
  dependsOn 'createJReleaserOutputDir'
}