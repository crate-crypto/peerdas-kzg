plugins {
    id 'java'
    id "java-test-fixtures"
    id "me.champeau.jmh" version "0.7.0"
    id "com.diffplug.spotless" version "6.17.0"
    id 'maven-publish'
}

group = 'ethereum.cryptography'
version = '0.0.1' // x-release-please-version

repositories {
    mavenCentral()
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    def jacksonVersion = "2.14.2"

    testFixturesImplementation("org.apache.tuweni:tuweni-units:2.3.1")
    testFixturesImplementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    testFixturesImplementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}")
}

// TODO: Re-evaluate whether we should keep this
test {
    useJUnitPlatform()
    dependsOn cleanTest
    testLogging.showStandardStreams = true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/crate-crypto/peerdas-kzg")
            credentials {
                username = System.getenv("GITHUB_GRADLE_USERNAME")
                password = System.getenv("GITHUB_GRADLE_PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}